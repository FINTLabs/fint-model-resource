plugins {
    id 'com.github.ben-manes.versions' version '0.51.0'
    id 'com.github.kt3k.coveralls' version '2.12.2'
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'

jar {
    archiveBaseName = 'fint-model-resource'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.release = 8
    options.compilerArgs += ['-Xlint:-options']
}

project.group = 'no.novari'

repositories {
    mavenLocal()
    maven {
        url = 'https://repo.fintlabs.no/releases'
    }
    mavenCentral()
}

dependencies {
    implementation('no.novari:fint-model-core:1.0.0')
    implementation('com.fasterxml.jackson.core:jackson-databind:2.20.0')
    
    compileOnly("org.projectlombok:lombok:1.18.40")
    testCompileOnly("org.projectlombok:lombok:1.18.40")
    annotationProcessor("org.projectlombok:lombok:1.18.40")
    testAnnotationProcessor("org.projectlombok:lombok:1.18.40")

    testImplementation("org.spockframework:spock-core:2.4-M6-groovy-4.0")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

publishing {
    repositories {
        maven {
            url = "https://repo.fintlabs.no/releases"
            credentials {
                username = System.getenv("REPOSILITE_USERNAME")
                password = System.getenv("REPOSILITE_PASSWORD")
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}